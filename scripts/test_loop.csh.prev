#!/bin/tcsh
#
# repeatedly send expose commands to ls4 control program
#
if ( $#argv != 6 ) then
  echo "syntax: test_loop.csh [shutter_flag (0,1)] [exptime(sec)] [fits_prefix] [data_dir] [clear_flag(0,1) [num_exposures]]"
  exit
else
  set shutter_flag = $argv[1]
  set exptime0 = $argv[2]
  set prefix = $argv[3]
  set data_dir = $argv[4]
  set clear_flag = $argv[5]
  set num_exp = $argv[6]
endif

if (! -e $LS4_CONTROL_ROOT/scripts/make_mosaics.csh ) then
   echo "no make_mosaics.csh"
   exit
endif

if ( $shutter_flag == 0 ) then
  set shutter_flag = "False"
else
  set shutter_flag = "True"
endif
set ls4_host = `hostname`
set exp_incr = 0  
set exp_delay = 0.0

if ( ! -e $data_dir ) then
   echo "data_dir $data_dir does not exist"
   exit
endif

cd $data_dir

# disk usage limit in percent
set DISK_LIMIT = 90

if ( $clear_flag == 1 ) then
  echo `date` "clearing 30 sec"
  set l = `echo "clear 30" | netcat -N $ls4_host $SERVER_PORT`
  echo `date` $l   
  if ( $l != "DONE") exit
endif

set l = `echo "vsub_on" | netcat -N $ls4_host $SERVER_PORT`
echo `date` $l   
if ( $l != "DONE") exit

echo `date` "taking $num_exp  exposures"

set n = 0
@ n_last = $num_exp + 1
set l = "DONE"
echo $l $n $n_last
while ( $l == "DONE" && $n < $n_last )
  set disk_usage =  `df -k /data | awk '{print $5}' | tail -n 1 | sed -e "s/%//g"`
  echo `date` "disk_usage " $disk_usage
  if ( $disk_usage > $DISK_LIMIT ) then
     echo `date` "disk_usage " $disk_usage " exceeds $DISK_LIMIT %"
     break
  endif
  set save_dir = `printf "exp_%05d" $n`
  if ( -e $save_dir ) then
     rm -rf $save_dir
  endif
  mkdir $save_dir
  @ n = $n + 1
  set exptime = `echo "scale=3; $exptime0 + $n * $exp_incr" | bc`
# set exp_mode = "single"

  if ( $n == 1 ) then
    set exp_mode = "first"
  else if ( $n == $n_last ) then
    set exp_mode = "last"
  else
    set exp_mode = "next"
  endif
  echo `date` $n $exp_mode start
  set l = `echo "expose $shutter_flag $exptime $prefix $exp_mode" | netcat -N $ls4_host $SERVER_PORT`
  echo `date` $n $exp_mode $l   
  #$LS4_CONTROL_ROOT/scripts/make_mosaics.csh testC 
  mv "$prefix"C*fits $save_dir
  sleep $exp_delay
end

echo `date` "shutting down"
set l = `echo "shutdown" | netcat -N $ls4_host $SERVER_PORT`
echo `date` "shutting down" $l


